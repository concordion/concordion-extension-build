buildscript {
    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/groups/public"}
        jcenter()
    }

    dependencies {
        classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.7.1'
        classpath 'net.researchgate:gradle-release:2.0.2'
        classpath 'org.ajoberstar:gradle-git:0.10.1'
    }
}

apply plugin: 'java'
apply plugin: 'wrapper'
apply plugin: 'net.researchgate.release'
apply plugin: nexus.NexusPlugin
apply plugin: org.ajoberstar.gradle.git.ghpages.GithubPagesPlugin

wrapper {
    gradleVersion = "2.2"
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.concordion:concordion:1.4.7'
}

compileJava {
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
}

group='org.concordion'

test {
    systemProperties['concordion.output.dir'] = "$reporting.baseDir/spec"
}

createReleaseTag.dependsOn uploadArchives

modifyPom {
    project {
        name project.name
        description project.description
        url "https://github.com/concordion/${project.name}"
        packaging 'jar'
        inceptionYear ext.inceptionYear

        scm {
            url "https://github.com/concordion/${project.name}.git"
            connection "scm:git:git://github.com/concordion/${project.name}.git"
            developerConnection "scm:git:git@github.com:concordion/${project.name}.git"
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.html'
                distribution 'repo'
            }
        }

        issueManagement{
            system 'GitHub Issues'
            url "https://github.com/concordion/${project.name}/issues"
        }

        developers ext.developers
    }
}

publishGhPages.dependsOn test
publishGhPages.dependsOn javadoc

task publishDocs(dependsOn: ['publishGhPages'])

task publishSnapshot(dependsOn: ['test', 'uploadArchives'])

task publishRelease(dependsOn: ['publishDocs', 'release'])
